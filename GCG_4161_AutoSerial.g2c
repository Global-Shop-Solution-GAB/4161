Program.Sub.ScreenSU.Start
Gui.F_Automation..Create
Gui.F_Automation..Caption("Serialization Automation")
Gui.F_Automation..Size(3000,1800)
Gui.F_Automation..MinX(0)
Gui.F_Automation..MinY(0)
Gui.F_Automation..Position(0,0)
Gui.F_Automation..BackColor(-2147483633)
Gui.F_Automation..MousePointer(0)
Gui.F_Automation..Event(UnLoad,F_Automation_UnLoad)
Gui.F_Automation.lblStart.Create(Label,"Starting Serial Number",True,1935,255,0,255,195,True,0,"Arial",8,-2147483633,0)
Gui.F_Automation.cmdOk.Create(Button)
Gui.F_Automation.cmdOk.Size(1005,375)
Gui.F_Automation.cmdOk.Position(255,900)
Gui.F_Automation.cmdOk.Caption("OK")
Gui.F_Automation.cmdOk.Event(Click,cmdOk_Click)
Gui.F_Automation.txtStartNo.Create(TextBox,"",True,2505,300,0,255,450,True,0,"Arial",8,-2147483643,1)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
Variable.Local.sHook.Declare(String)

Variable.Local.sHook.Set(Variable.Caller.Hook)

'Cases by hook:
Function.Intrinsic.Control.SelectCase(Variable.Local.sHook)

'If populate hook, change Script 1 label to "Auto Serialize"
Function.Intrinsic.Control.Case("31825")
	Variable.Passed.000006.Set("Auto Serialize")

'If Script 1 click hook, Show screen
Function.Intrinsic.Control.Case("31826")
	Gui.F_Automation..Show

Function.Intrinsic.Control.EndSelect

Program.Sub.Main.End

Program.Sub.cmdOk_Click.Start
Variable.Local.sSerial.Declare(String)
Variable.Local.sSql.Declare(String)
Variable.Local.iCount.Declare(Long)
Variable.Local.iSerial.Declare(Long)
Variable.Local.iIndex.Declare(Long)
Variable.Local.bNo.Declare(Boolean)
Variable.Local.iEntered.Declare(Long)
V.Local.iLength.Declare(Long)

Variable.Local.sSerial.Set(Variable.Screen.F_Automation!txtStartNo.Text)

'Check to see if the value entered is a number
Function.Intrinsic.Control.If(Variable.Local.sSerial.Trim, =, "")
	Function.Intrinsic.Control.ExitSub
Function.Intrinsic.Control.EndIf

Function.Intrinsic.Math.IsNumeric(Variable.Local.sSerial, Variable.Local.bNo)
Function.Intrinsic.Control.If(Variable.Local.bNo, =, False)
	Function.Intrinsic.UI.Msgbox("Please enter a numeric value for the Serial Number", "Invalid Input")
	Function.Intrinsic.Control.ExitSub
Function.Intrinsic.Control.Else
	Gui.F_Automation..Visible(False)
Function.Intrinsic.Control.EndIf

'Get the number of Serial numbers needed.
Variable.Local.iCount.Set(Variable.Passed.000026)
Variable.Local.iEntered.Set(Variable.Passed.000027)
Function.Intrinsic.Math.Sub(Variable.Local.iCount, Variable.Local.iEntered, Variable.Local.iCount)

V.Local.iLength.Set(V.Local.sSerial.Length)

Variable.Local.iSerial.Set(Variable.Local.sSerial.Long)

Function.Intrinsic.BDF.Load("AUX001", "AUX001")

Function.Intrinsic.Control.If(Variable.Local.iCount, >, 0)
	Function.Intrinsic.Control.For(Variable.Local.iIndex, Variable.Local.iCount, 1, -1)
		'Add number to the BDF
		Function.Intrinsic.String.Build("{0}|~| ", Variable.Local.sSerial, Variable.Local.sSql)
		Function.Intrinsic.BDF.WriteRow("AUX001", Variable.Local.sSql)
		Function.Intrinsic.BDF.Save("AUX001", "AUX001")
		
		'Create the next number
		Function.Intrinsic.Math.Add(Variable.Local.sSerial, 1, Variable.Local.sSerial)
		F.Intrinsic.String.LPad(V.Local.sSerial,"0",V.Local.iLength,V.Local.sSerial)
	Function.Intrinsic.Control.Next(Variable.Local.iIndex)
Function.Intrinsic.Control.EndIf

Variable.Passed.000027.Set(Variable.Local.iCount)

Program.Sub.cmdOk_Click.End

Program.Sub.F_Automation_UnLoad.Start
Function.Intrinsic.Control.End

Program.Sub.F_Automation_UnLoad.End

Program.Sub.Comments.Start
${$0$}$Work Order Serialization Automation$}$SISCO$}$9/19/2016 8:36:52 AM$}$False
${$3$}$0$}$$}$0$}$-1$}$$}$12:00:00 AM$}$This project was created by FEQ on 09/21/2016
This will be attached to the Script 1 button and will ask the customer for an innitial Serial number and the number of serial numbers to be created automatically. The GAB script will then create that many serial numbers by adding one to the previous serial number.
Program.Sub.Comments.End